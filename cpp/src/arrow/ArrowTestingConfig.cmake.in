# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#
# This config sets the following variables in your project::
#
#   ArrowTesting_FOUND - true if Arrow testing library found on the system
#
# This config sets the following targets in your project::
#
#   ArrowTesting::arrow_testing_shared - for linked as shared library if shared library is built
#   ArrowTesting::arrow_testing_static - for linked as static library if static library is built

@PACKAGE_INIT@

set(ARROW_GTEST_SOURCE "@GTest_SOURCE@")

include(CMakeFindDependencyMacro)
find_dependency(Arrow)

if(DEFINED CMAKE_MODULE_PATH)
  set(ARROW_TESTING_CMAKE_MODULE_PATH_OLD ${CMAKE_MODULE_PATH})
else()
  unset(ARROW_TESTING_CMAKE_MODULE_PATH_OLD)
endif()
set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")
if("${ARROW_GTEST_SOURCE}" STREQUAL "SYSTEM")
  find_dependency(GTestAlt)
endif()
if(DEFINED ARROW_TESTING_CMAKE_MODULE_PATH_OLD)
  set(CMAKE_MODULE_PATH ${ARROW_TESTING_CMAKE_MODULE_PATH_OLD})
  unset(ARROW_TESTING_CMAKE_MODULE_PATH_OLD)
else()
  unset(CMAKE_MODULE_PATH)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/ArrowTestingTargets.cmake")

arrow_keep_backward_compatibility(ArrowTesting arrow_testing)

check_required_components(ArrowTesting)

arrow_show_details(ArrowTesting ARROW_TESTING)
